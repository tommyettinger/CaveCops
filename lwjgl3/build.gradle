buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "org.beryx:badass-runtime-plugin:1.8.5" // plugin dependencies
	}
}
apply plugin: 'org.beryx.runtime'
apply plugin: 'application'

def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)

sourceSets.main.resources.srcDirs += [ rootProject.file('assets').path ]
mainClassName = 'com.github.tommyettinger.lwjgl3.Lwjgl3Launcher'
sourceCompatibility = 11

dependencies {
  api project(':core')
  api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
  api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

import org.gradle.internal.os.OperatingSystem

run {
	workingDir = rootProject.file('assets').path
	setIgnoreExitValue(true)
	if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
		// Required to run LWJGL3 Java apps on MacOS
		jvmArgs += "-XstartOnFirstThread"
	}
}
jar {
	archiveFileName = "${appName}-Demo.jar"
	destinationDirectory = file("$project.buildDir/lib")
	
	dependsOn configurations.runtimeClasspath
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } } 
	manifest {
		attributes 'Main-Class': project.mainClassName
	}
}

//jpackageImage.dependsOn jar

task proguard(type: proguard.gradle.ProGuardTask) {
	outputs.upToDateWhen { false }
	configuration 'proguard-rules.pro'
	//printmapping 'build/lib/out.map'
	if (System.getProperty('java.version').startsWith('1.')) {
		libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
	} else {
		libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
	}
	injars "build/lib/${appName}-Demo.jar"
	outjars "build/lib/protected.jar"
}

proguard.dependsOn jar

jpackageImage.dependsOn proguard

runtime {
	options.set(['--strip-debug',
				 '--compress', '2',
				 '--no-header-files',
				 '--no-man-pages',
				 '--strip-native-commands',
				 '--vm', 'server'])
	modules.set(['java.base',
				 'java.desktop',
				 'jdk.unsupported'])
	distDir.set(file(buildDir))
	jpackage {
		imageName = 'CaveCops'
		skipInstaller = true
		jpackageHome = javaHome.getOrElse("")
		mainJar = 'protected.jar'// jar.archiveFileName.get()
		if (osName.contains('windows')) {
			imageOptions = ["--icon", "../logo.ico"]
		} else if (osName.contains('linux')) {
			imageOptions = ["--icon", "../logo.png"]
		} else if (osName.contains('mac')) {
			imageOptions = ["--icon", "../logo.icns"]
		}
	}
}

jpackageImage.doLast {
	file("build/jpackage/CaveCops/app/${appName}-Demo.jar").delete()
}
